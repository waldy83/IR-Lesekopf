>D 40
;*****
; Gaszähler - DMY
;
; Chart Script by ottelo.jimdo.de - modified by waldy
;
; Tested by waldy
;*****
; - Haken bei "Skript aktivieren" setzen
; - Skript auswaehlen
; - Speichern anklicken
;*****
; Faktor
; 1 imp ≙ 0,01 m³  =>  100
; 1 imp ≙ 0,1 m³   =>  10
; 1 imp ≙ 1,0 m³   =>  1
factor=100

; -- ARRAYS --
; Tagesnetzbezug Tabelle 1-31
M:p:dcon=0 31
; Monatsverbrauch Tabelle 1-12
M:p:mcon=0 12

; -- VARS --
; Netzbezug [m3]
GasTotal=0

; Tages, Monats, Jahres Verbrauch
p:mval=0
p:dval=0
p:yval=0

tmp=0
utm="00d 00h 00m"
hour=0
yr=0
da=1
save=0

; -- BOOT --
>B
=>sensor53 r
;Deaktiviere MQTT beim Start, verhindert das falsche Werte gesendet werden
smlj=0

;Anfangszählerstand
=>sensor53 c1 0

;-- SML --
>M 1
; Setupline
+1,14,c,1,-500,Gas
1,1-0:1.8.0*255(@%factor%,Gesamtverbrauch,m3,total,18)

; -- JEDE SEKUNDE --
>S
if (save==1) {
save=0
svars
print Daten wurden gespeichert
}

; Start MQTT erst wenn Daten vom Zaehler kommen
if (sml[1]>0) {
smlj=1
}

; Warte auf NTP
if (year<2020) {
print NTP not ready
break
}

; alle 60s
if (upsecs%60==0) {
hour=hours
yr=year
;Zeit seit letztem Boot
utm=s(2.0(int(uptime/1440)))+"d "+s(2.0(int(uptime/60)%24))+"h "+s(2.0(uptime%60))+"m"

; kopiere Netzbezug [m3] vom Zaehler (aus SML Variable)
GasTotal=sml[1]
; Tagesverbrauch [m3]
dcon[day]=GasTotal-dval
; Monatsverbrauch [m3]
mcon[month]=GasTotal-mval

; Tagesverbrauch Berechnung um Mitternacht
if ((chg[hour]>0) and (hour==0)) {
if (day>1) {
da=day
} else {
; Monatswechsel
for tmp (da+1) 31 1
dcon[tmp]=0
next
; monthly values
mval=GasTotal
}
if (chg[yr]>0) {
; Jahreswechsel
yval=GasTotal
}
; daily values
dval=GasTotal
; nur 1x um Mitternacht speichern
svars 
}
}

; WEB INTERFACE
>W

; web button
bu(save "gespeichert!" "Daten sofort speichern")

; Verbrauch
Tagesverbrauch{m}%2(GasTotal-dval)% m3
Monatsverbrauch{m}%2(GasTotal-mval)% m3
Jahresverbrauch{m}%2(GasTotal-yval)% m3

; Zeit
Datum{m}%s(2.0day)%.%s(2.0month)%.%s(2.0year)% - %s(2.0hours)%:%s(2.0mins)%:%s(2.0secs)%
Uptime{m}%utm%

